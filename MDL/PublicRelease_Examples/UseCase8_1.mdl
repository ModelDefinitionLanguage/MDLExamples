# Warfarin model with Between Occasion Variability (BOV)
#
# Uses ODEs. Transformation of WT covariate in COVARIATES block
# Uses "general" type (Type 2 Gaussian) relationship for individual parameters 
# 


warfarin_PK_BOV_dat = dataObj{
	DECLARED_VARIABLES{INPUT_KA::dosingTarget Y::observation}

  DATA_INPUT_VARIABLES{
      ID : { use is id }  
      TIME : { use is idv }
      WT : { use is covariate }
      AGE : { use is covariate }
      SEX : { use is catCov withCategories {female when 1, male when 0} }
      AMT : { use is amt, variable = INPUT_KA}
      OCC : { use is varLevel }
      DV : { use is dv, variable = Y }
      MDV : { use is mdv }
   }# end DATA_INPUT_VARIABLES

	SOURCE {
	    srcfile: {file = "warfarin_conc_bov_P4_sort.csv",
        		  inputFormat is nonmemFormat }
	} # end SOURCE
} # end data object


warfarin_PK_BOV_par = parObj{
   STRUCTURAL{
      POP_CL:{value=0.1, lo=0.001} # L/h/70kg
      POP_V:{value=8, lo=0.001} # L/70kg
      POP_KA:{value=2, lo=0.001} # h-1
      POP_TLAG:{value=1, lo=0.001} # h
	  BETA_CL_WT : { value = 0.75, fix = true }
	  BETA_V_WT : { value = 1, fix = true } 
   }# end STRUCTURAL

   VARIABILITY{
      BSV_CL : {  value=0.1 }
      BSV_V : { value=0.1 }
	  COV_BSV_CL_V : { value = 0.01 }

      BOV_CL : { value=0.1}
      BOV_V : { value=0.1}

      BSV_KA:{value=0.1}
      BSV_TLAG:{value=0.01, fix=true}

      RUV_PROP:{value=0.1, lo=0}
      RUV_ADD:{value=0.224, lo=0} # mg/L
   }# end VARIABILITY

} # end of parameter object

warfarin_PK_BOV_mdl = mdlObj{
   IDV { TIME }

   COVARIATES{
	WT
	logtWT = ln(WT/70)
   }

   VARIABILITY_LEVELS{
	ID : { level=3, type is parameter }
	OCC : { level=2, type is parameter }
   	DV : { level=1, type is observation }
   }

   STRUCTURAL_PARAMETERS{
      #THETA
      POP_CL
      POP_V
      POP_KA
      POP_TLAG
	  BETA_CL_WT
	  BETA_V_WT
   }# end STRUCTURAL_PARAMETERS

   VARIABILITY_PARAMETERS{
      #OMEGA
      BSV_CL
      BSV_V
 	  COV_BSV_CL_V     
      BOV_CL
      BOV_V
      BSV_KA
      BSV_TLAG
      RUV_PROP
      RUV_ADD
   }# end VARIABILITY_PARAMETERS

	GROUP_VARIABLES{
      # Covariate model
      GRPCL=ln(POP_CL) + BETA_CL_WT*logtWT  ## Type 1 - Equation type
      GRPV=ln(POP_V) + BETA_V_WT*logtWT
      GRPKA=ln(POP_KA)
      GRPLG=ln(POP_TLAG)
   }# end GROUP_VARIABLES
   
   RANDOM_VARIABLE_DEFINITION(level=ID){
      eta_BSV_CL~ Normal(mean=0, var=BSV_CL)
      eta_BSV_V~ Normal(mean=0, var=BSV_V)
      :: {type is covariance, rv1=eta_BSV_CL, rv2=eta_BSV_V, value=COV_BSV_CL_V}
      eta_BSV_KA~ Normal(mean=0, var=BSV_KA)
      eta_BSV_TLAG~ Normal(mean=0, var=BSV_TLAG)
   }# end RANDOM_VARIABLE_DEFINITION

   RANDOM_VARIABLE_DEFINITION(level=OCC){
      eta_BOV_CL~ Normal(mean=0, var=BOV_CL)
      eta_BOV_V~ Normal(mean=0, var=BOV_V)
   }# end RANDOM_VARIABLE_DEFINITION

   INDIVIDUAL_VARIABLES{
      # Individual parameters
      ## Type 2 - Guassian with general covariate model
	  CL : {type is general, trans is ln, grp=GRPCL, ranEff=[eta_BSV_CL,eta_BOV_CL] } 
	  V  : {type is general, trans is ln, grp=GRPV, ranEff=[eta_BSV_V,eta_BOV_V] }
      KA : {type is general, trans is ln, grp=GRPKA, ranEff=eta_BSV_KA }
      TLAG : {type is general, trans is ln, grp=GRPLG, ranEff=eta_BSV_TLAG }
   }# end INDIVIDUAL_VARIABLES

 	MODEL_PREDICTION {
		COMPARTMENT {
			INPUT_KA      : {type is depot, to=CENTRAL, ka=KA, tlag=TLAG}
			CENTRAL       : {type is compartment}
                     	 :: {type is elimination, from=CENTRAL, v=V, cl=CL}
   		}# end COMPARTMENT

	    CC = CENTRAL / V 
	} # end MODEL_PREDICTION

	RANDOM_VARIABLE_DEFINITION(level=DV) {
		EPS_Y ~ Normal(mean = 0, var = 1) 
	} # end RANDOM_VARIABLE_DEFINITION 
	
	OBSERVATION {
	    Y : {type is combinedError1, additive = RUV_ADD, proportional = RUV_PROP, eps = EPS_Y, prediction = CC }
	} # end OBSERVATION

} # end of model object

warfarin_PK_BOV_task = taskObj{

      ESTIMATE {
		set algo is saem
      }
}# end of taskObj

warfarin_PK_BOV_mog=mogObj{
	OBJECTS{
		warfarin_PK_BOV_mdl : { type is mdlObj }
		warfarin_PK_BOV_par : { type is parObj }
		warfarin_PK_BOV_dat : { type is dataObj }
		warfarin_PK_BOV_task : { type is taskObj }
	}
}
