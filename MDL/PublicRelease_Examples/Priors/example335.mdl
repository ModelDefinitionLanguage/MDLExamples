# Prior_Example335 : Parameters POP_V, POP_K are correlated 
# Dosing regimen: single IV bolus administration (at time 0)
# Structural model : 1 compartment model (V, K)
# 1 cmpt (V) + linear elimination (K)
# C(t) = Dose / V*exp(-K*t)
# Variability model: 
#   - IIV on V, K.
##    ETA_K ~ Normal(mean, cov); ETA_V ~ Normal(mean, stdev)
#   - Additive residual error model
#########
## PRIOR SPECIFICATION:
## Population mean parameters are correlated
## lPOP_P ~ MultivariateNormal1(mean=lMU_POP_P, covarianceMatrix=SIGMA_POP_P)
##
## Population between subject variabilities are independent:
## TAU_K ~ Gamma2(shape=a_OMEGA_K, rate=b_OMEGA_K)
## OMEGA_K=1/TAU_K etc.
##
## Residual error
## TAU ~ Gamma2(a_POP_T, b_POP_T)

example331_dat = dataObj {
   DECLARED_VARIABLES{ DOSE::dosingTarget Y::observation}
      DATA_INPUT_VARIABLES{
      ID:{use is id}
      AMT:{use is amt, variable=DOSE}
      TIME:{use is idv}
      DV:{use is dv, variable=Y}
      MDV : {use is mdv}
   }# end DATA_INPUT_VARIABLES

   SOURCE{
   mysrcfile:{file="TestData.csv",
      inputFormat is nonmemFormat}
   }# end SOURCE
     
}

example335_pri = priorObj{

  PRIOR_PARAMETERS{
		# prior on "THETA"
		MU_POP_K = 0.1
		MU_POP_V = 8
		
        SIGMA_POP_P = triangle([0.1,0.5e-4,0.1],2,true)
 		
        # prior on "OMEGA"
		a_OMEGA_K = 1
		b_OMEGA_K = 0.01
		a_OMEGA_V = 1
		b_OMEGA_V = 0.01
		a_OMEGA_T = 1
		b_OMEGA_T = 0.01
		
		# prior on "SIGMA"
        a_POP_T = 1
		b_POP_T = 0.01
   } # end PRIOR

   PRIOR_VARIABLE_DEFINITION{

		# prior on "THETA"
	    lMU_POP_P = [ln(MU_POP_K), ln(MU_POP_V)]   	       
	 	lPOP_P ~ MultivariateNormal1(mean=lMU_POP_P, covarianceMatrix=SIGMA_POP_P)
	 	
	    POP_K = exp(lPOP_P[1])
	    POP_V = exp(lPOP_P[2]) 
	    
	 	# prior on "OMEGA"	
      TAU_K ~ Gamma2(shape=a_OMEGA_K, rate=b_OMEGA_K)
      OMEGA_K=1/TAU_K
      TAU_V ~ Gamma2(shape=a_OMEGA_V, rate=b_OMEGA_V)
      OMEGA_V=1/TAU_V 
      TAU_T ~ Gamma2(shape=a_OMEGA_T, rate=b_OMEGA_T)
      OMEGA_T=1/TAU_T 

      # prior on "SIGMA"  
      POP_T ~ Gamma2(shape=a_POP_T, rate=b_POP_T)                
    } # end PRIOR_VARIABLE_DEFINITION

} # end of parameter object

example331_mdl = mdlObj{
	IDV{ T }
	
	VARIABILITY_LEVELS{
		ID:{level=2, type is parameter}
		DV:{level=1, type is observation}
	}

   STRUCTURAL_PARAMETERS{
      POP_K
      POP_V
      POP_T
   }# end STRUCTURAL_PARAMETERS

   VARIABILITY_PARAMETERS{
      OMEGA_K
      OMEGA_V
      OMEGA_T
   }# end VARIABILITY_PARAMETERS


   RANDOM_VARIABLE_DEFINITION(level=ID){
      eta_K ~ Normal( mean=0, var=OMEGA_K)
      eta_V ~ Normal( mean=0, var=OMEGA_V)    
      eta_T ~ Normal( mean=0, var=OMEGA_T)   
   }# end RANDOM_VARIABLE_DEFINITION

   INDIVIDUAL_VARIABLES{
      K : {type is linear, trans is ln, pop = POP_K, ranEff=eta_K}
      V : {type is linear, trans is ln, pop = POP_V, ranEff=eta_V}
      TAU : {type is linear, trans is ln, pop = POP_T, ranEff=eta_T}
      SD_ADD = 1/sqrt(TAU)  
   }# end INDIVIDUAL_VARIABLES

   MODEL_PREDICTION{   
      DOSE :: dosingVar
      C=DOSE/V*exp(-K*T)
   }# end MODEL_PREDICTION   
   
   RANDOM_VARIABLE_DEFINITION(level=DV){
   		eps ~ Normal( mean=0, var=1)
   	}
   
   OBSERVATION{
      Y : {type is additiveError, additive=SD_ADD, eps=eps, prediction= C}
   }# end OBSERVATION  

} # end of model object

example331_task = taskObj{   
	ESTIMATE{ 
		set algo is mcmc
        TARGET_SETTINGS(target="BUGS"){
            set 
            nchains = 1,
            burnin = 1000,
            niter = 5000,
    		parameters = "POP_K, POP_V, OMEGA_K, OMEGA_V, OMEGA_T, SD_ADD, V,K",
          	winbugsgui="false"
        }
}
} # end of task object
	
example335 = mogObj{
	OBJECTS{
		example331_dat : { type is dataObj }
		example331_mdl : { type is mdlObj }
		example335_pri : { type is priorObj }
		example331_task : { type is taskObj }
	}
}